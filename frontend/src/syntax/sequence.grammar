@skip { space | Comment } 

@top Program {
    titleLine?
    participantLine*
    messageFull*
}

participantLine {
    @specialize[@name="participant"]<ParticipantName, "participant"> ":" ParticipantName
}

messageFull {
    messageLine labelLine*
}

messageLine {
    ParticipantName Arrow Delay? ParticipantName
}

@skip { } {
    titleLine {
        @specialize[@name="title"]<ParticipantName, "title"> ":" TitleText lineEnd
    }
    labelLine {
        @specialize[@name="label"]<ParticipantName, "label"> ":" LabelText lineEnd
    }
    Arrow { "->" | "->>" | "-->" | "-->>" }
    Delay { "(" digits ")" }
}

@tokens {
    lineEnd { $[\n] }
    space { $[ \t\n]+ }
    Comment { "#" ![\n]* }
    digits { std.digit+ }

    "participant"
    "label"
    "title"

    ParticipantName {
        std.asciiLetter (std.asciiLetter | std.digit)*
    }
    LabelText { ![\n]+ }
    TitleText { ![\n]+ }
}